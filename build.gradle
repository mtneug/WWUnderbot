/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'mtneug' at '13.12.15 16:03' with Gradle 2.9
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.9/userguide/tutorial_java_projects.html
 */

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.palominolabs.gradle.task:gradle-git-clone-task:0.0.2'
  }
}

apply plugin: 'java'
apply plugin: 'application'

applicationName = 'WWUnderbot'
mainClassName = 'com.theaigames.blockbattle.ThreadedBlockbattle'
version = '1.0'

sourceCompatibility = 8

task cloneBlockbattleEngineRepo(type: com.palominolabs.gradle.task.git.clone.GitCloneTask) {
  dir = file("$buildDir/blockbattle-engine")
  uri = 'https://github.com/mtneug/blockbattle-engine'
  treeish = 'origin/master'
  reset = true
}

task compileBlockbattleEngine(dependsOn: 'cloneBlockbattleEngineRepo', type: JavaCompile) {
  source = fileTree(dir: "$buildDir/blockbattle-engine/src", include: '**/*.java')
  classpath = files("$buildDir/blockbattle-engine/src")
  destinationDir = file("$buildDir/classes/main")
  dependencyCacheDir = file("$buildDir/classes/main")
}

classes.dependsOn compileBlockbattleEngine

task run(overwrite: true, type: JavaExec) {
  main = mainClassName
  classpath = sourceSets.main.runtimeClasspath
  args = [
    "Main",
    "Main"
  ]
}

task server_run(dependsOn: 'classes', type: JavaExec) {
  main = 'com.theaigames.blockbattle.Blockbattle'
  classpath = sourceSets.main.runtimeClasspath
  args = [
    "java -cp $buildDir/classes/main Main",
    "java -cp $buildDir/classes/main Main"
  ]
}
